group 'com.complexible.stardog.docs.corenlp'
version '1.0'

apply plugin: 'java'

sourceCompatibility = 1.8

ext {
	stardogVersion = '6.2.0'
	corenlpVersion = '3.9.1'
	junitVersion = '4.12'
}

repositories {
    maven { url 'http://maven.stardog.com' }
    mavenCentral()
    mavenLocal()
}

configurations {
	localDeps
}

dependencies {
	compile group: 'com.complexible.stardog', name: 'server', version: stardogVersion
	compile group: 'com.complexible.stardog', name: 'client-http', version: stardogVersion

	localDeps group: 'edu.stanford.nlp', name: 'stanford-corenlp', version: corenlpVersion
	localDeps group: 'edu.stanford.nlp', name: 'stanford-corenlp', classifier: 'models', version: corenlpVersion
	localDeps group: 'edu.stanford.nlp', name: 'stanford-corenlp', classifier: 'models-english', version: corenlpVersion
	localDeps group: 'edu.stanford.nlp', name: 'stanford-corenlp', classifier: 'models-english-kbp', version: corenlpVersion

	compile configurations.localDeps

    testCompile group: 'junit', name: 'junit', version: junitVersion
	testCompile group: 'com.complexible.stardog', name: 'client-embedded', version: stardogVersion
}

task copyDeps(type: Copy) {
	into "$buildDir/libs"
	from configurations.localDeps
}

task fatJar(type: Jar) {
	baseName = project.name + '-all'
	from { configurations.localDeps.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

test {
	systemProperty "stardog.home", System.getProperty("stardog.home")
	maxHeapSize = "8g"
}